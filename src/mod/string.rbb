%load char~();;
~:pack = %load rbbpack
~:tostrdata = %load rbbconsttostr

~:mk_string = ~fn~ strdata {
  !(:strdata = ~strdata, mk_string = ~mk_string, pack = ~pack).{
    # Make the string respond to the "string" interface

    ~:msg = $
    !~msg == <<? if_true ~.{
      !(~merge:).{
        ~:ifacename = $
        !~ifacename == string if_true ().{!?t} ~.{
          !~strdata <<? (~ifacename)
        }
      }
    } ~.{
      !~msg == concat if_true ~.{
        !(~merge:).{
          ~:other = $
          ~other <<? string neg if_true_raise:
            error = string_expected,
            got = ~other

          !~mk_string (~strdata concat (~pack (~other)))
        }
      } ~.{
        !~strdata (~msg)
      }
    }
  }
}

%lit:
  trigger = '"',
  eval = ~fn~ stream {
    ~:strdata = ~pack (|)

    ~stream skip
    ~while~ {!~stream char_val /= '"'} {
      ~:strdata = ~strdata concat (~pack|~read_char (~stream))
      ~stream skip
    }

    !~mk_string (~strdata)
  };;

~:visited_to_string = ~fn~ el parent visited {
  ~:contains = ?f, i = 0
  ~while~ {!~i < (~visited len) and (~contains neg)} {
    ~:contains = ~visited (~i) == (~el)
    ~:i = ~i + 1
  }

  ~contains if_true ~.{
    ~:el_to_string = "..."
  } ~.{
    ~:el_to_string = ~obj_to_string_with_visits (~el) (~visited concat|~parent)
  }

  !~el_to_string
}

~:listable_to_string = ~fn~ list visited {
  ~:str = "(|"

  ~foreach~ (~list) el {
    ~:str = ~str concat (~visited_to_string (~el) (~list) (~visited))

    ~last_iteration neg if_true ~.{
      ~:str = ~str concat ", "
    }
  }

  ~:str = ~str concat ")"
  !~str
}

~:table_to_string = ~fn~ table visited {
  ~:str = "(:"

  ~foreach~ (~table keys) k {
    ~:str =
      (~str concat (~obj_to_string (~k))
        concat " = " concat (
          ~visited_to_string (~table (~k)) (~table) (~visited)))

    ~last_iteration neg if_true ~.{
      ~:str = ~str concat ", "
    }
  }

  ~:str = ~str concat ")"
  !~str
}

~:obj_to_string = ~fn~ obj {
  !~obj_to_string_with_visits (~obj) (|)
}

~:obj_to_string_with_visits = ~fn~ obj visited {
  !~obj << == if_true ~.{
    !~obj <<? string if_true ~.{
      !"\"" concat (~obj) concat "\""
    } ~.{
      !~obj << to_string and (~obj <<? block neg) if_true ~.{
        !~obj to_string  # TODO pass visited info to to_string
      } ~.{
        !(~obj == () or
          (~obj <<? numeric) or
          (~obj <<? symbol) or
          (~obj <<? boolean)) if_true ~.{
            !~mk_string (~tostrdata (~obj))
          } ~.{
            !~obj <<? listable if_true ~.{
              !~listable_to_string (~obj) (~visited)
            } ~.{
              !~obj <<? table if_true ~.{
                !~table_to_string (~obj) (~visited)
              } ~.{
                !~obj <<? block if_true ~.{
                  !"[block]"
                } ~.{
                  # TODO allow hooks
                  !"[unknown]"
                }
              }
            }
          }
      }
    }
  } ~.{
    !"[opaque]"
  }
}
